plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    def appName = "RansaApp"
    namespace 'com.app.develop.ransapp'
    compileSdk 33

    defaultConfig {
        applicationId "com.ransa.app"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    variantFilter { variant ->
        if (variant.buildType.name == 'debug' || variant.buildType.name == 'release')
            setIgnore(true)
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def buildType = variant.buildType.name.toUpperCase()
            def date = new Date().format('yyyyMMdd')
            def versionName = variant.versionName
            def versionCode = variant.versionCode
            outputFileName = "Ransa-${buildType}-${date}-${versionName}-(${versionCode}).apk"
        }
    }

    signingConfigs {
        dev {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }

        prod {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        dev {
            versionNameSuffix ".0"
            signingConfig signingConfigs.dev
            debuggable true
            applicationIdSuffix ".dev"
            minifyEnabled true
            manifestPlaceholders = [marca_prefix: "${appName}", build_prefix: 'DEV']
        }

        preProd {
            versionNameSuffix ".0"
            signingConfig signingConfigs.prod
            debuggable true
            minifyEnabled false
            manifestPlaceholders = [marca_prefix: "${appName}", build_prefix: '']
        }

        prod {
            versionNameSuffix ".0"
            lintOptions {
                checkReleaseBuilds false
                abortOnError false
            }
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.prod
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [marca_prefix: "${appName}", build_prefix: '']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    //Hilt
    implementation 'com.google.dagger:hilt-android:2.38.1'
    implementation 'androidx.hilt:hilt-common:1.0.0'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'
    implementation 'androidx.security:security-crypto-ktx:1.1.0-alpha04'
    kapt 'com.google.dagger:hilt-android-compiler:2.38.1'
    //LiveData
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'

    //Inspector HTTP
    implementation 'com.github.chuckerteam.chucker:library:3.5.2'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'

    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.zxing:core:3.4.0'

    //Firebase
    implementation platform("com.google.firebase:firebase-bom:29.0.2")
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-messaging'

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}